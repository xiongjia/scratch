idea.module {
  excludeDirs += file("gradle")
  excludeDirs += file(".settings")
  excludeDirs += file("bin")
}

idea.project {
  jdkName = "1.8"
  languageLevel = 'JDK_1_8'
  vcs = "Git"
}

idea.project.ipr {
  withXml { xmlProvider ->
    def iprNode = xmlProvider.asNode()
    ideaActivateGradle(iprNode)
    ideaActivateGit(iprNode)
    ideaActivateCheckstyle(iprNode)
  }
}

ext {
  ideaActivateGradle = { Node iprNode ->
    // Activate 'Gradle' plugin
    def settings = iprNode.component.find { it.'@name' == 'GradleSettings' }
    if (settings == null) {
      settings = iprNode.appendNode('component', [name: 'GradleSettings'])
      settings.appendNode('option',
        [name: 'linkedProjectPath', value: '$PROJECT_DIR$/build.gradle'])
    }
  }

  ideaActivateGit = { Node iprNode ->
    // Activate 'git' as VCS
    def vcsMappings = iprNode.component.find { it.'@name' == 'VcsDirectoryMappings' }
    vcsMappings.mapping.@vcs = 'Git'
  }

  ideaActivateCheckstyle = { Node iprNode ->
    // Activate and config 'Checkstyle' plugin
    def checkstyle = iprNode.component.find { it.'@name' == 'CheckStyle-IDEA' }
    if (checkstyle == null) {
      checkstyle = iprNode.appendNode('component', [name: 'CheckStyle-IDEA'])
      def builder = new NodeBuilder()
      def option = builder.option(name: 'configuration') {
        map {
          entry(key: 'active-configuration',
                value: 'PROJECT_RELATIVE:$PROJECT_DIR$/config/checkstyle/checkstyle.xml:tatooine')
          entry(key: 'check-nonjava-files', value: false)
          entry(key: 'check-test-classes', value: true)
          entry(key: 'property-1.samedir', value: 'config/checkstyle')
          entry(key: 'suppress-errors', value: false)
          entry(key: 'thirdparty-classpath', value: '')
        }
      }
      checkstyle.append option
    }
  }
}

